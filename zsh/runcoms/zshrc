# origin
if [[ -s "$HOME/.zprezto/runcoms/zshrc" ]]; then
  source "$HOME/.zprezto/runcoms/zshrc"
fi

# modules
source "$HOME/.zsh/modules/zshrc"

# search history with peco
function peco-select-history() {
    local tac
    if which tac > /dev/null; then
        tac="tac"
    else
        tac="tail -r"
    fi
    BUFFER=$(\history -n 1 | \
        eval $tac | \
        peco --query "$LBUFFER")
    CURSOR=$#BUFFER
    zle clear-screen
}
zle -N peco-select-history
bindkey '^r' peco-select-history

# search ghq src with peco
function peco-ghq () {
  local selected_dir=$(ghq list -p | peco --query "$LBUFFER")
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-ghq

# search ghq src and bookmarks with peco
function peco-ghq-and-bookmarks () {
  local selected_dir=$(cat <(ghq list -p) "$HOME/.bookmarkdirs" | peco --query "$LBUFFER")
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-ghq-and-bookmarks

if [ -f "$HOME/.bookmarkdirs" ]; then
  bindkey '^ ' peco-ghq-and-bookmarks
else
  bindkey '^ ' peco-ghq
fi

# search tmuxinator with peco
function peco-tmuxinator () {
  local selected_session=$(tmuxinator list | tail -n +2 | tr -s ' ' '\n' | peco --query "$LBUFFER")
  if [ -n "$selected_session" ]; then
    BUFFER="tmuxinator start ${selected_session}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-tmuxinator
bindkey '^z^z' peco-tmuxinator

# search tmuxinator edit with peco
function peco-tmuxinator-edit () {
  local selected_session=$(tmuxinator list | tail -n +2 | tr -s ' ' '\n' | peco --query "$LBUFFER")
  if [ -n "$selected_session" ]; then
    BUFFER="tmuxinator new ${selected_session}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-tmuxinator-edit
bindkey '^ze' peco-tmuxinator-edit

# emacs like keybind
bindkey "^A" beginning-of-line
bindkey "^B" backward-char
bindkey "^D" delete-char-or-list
bindkey "^E" end-of-line
bindkey "^F" forward-char
bindkey "^G" send-break
bindkey "^K" kill-line
bindkey "^L" clear-screen
bindkey "^N" history-substring-search-down
bindkey "^O" accept-line-and-down-history
bindkey "^P" history-substring-search-up
bindkey "^S" history-incremental-pattern-search-forward
bindkey "^T" transpose-chars

# ローカルの設定ファイルがあればそれを読み込み
if [ -f "$HOME/.zshrc.local" ]; then
  source "$HOME/.zshrc.local"
fi

if [ -d "$ZSH_COMPLETION_DIR" ]; then
  fpath=($ZSH_COMPLETION_DIR $fpath)
fi

# ls
## from `:prezto:module:utility`
if is-callable 'dircolors'; then
  # GNU Core Utilities

  if zstyle -T ':prezto:module:utility:ls' dirs-first; then
    alias ls="${aliases[ls]:-ls} --group-directories-first"
  fi

  if zstyle -t ':prezto:module:utility:ls' color; then
    # Call dircolors to define colors if they're missing
    if [[ -z "$LS_COLORS" ]]; then
      if [[ -s "$HOME/.dir_colors" ]]; then
        eval "$(dircolors --sh "$HOME/.dir_colors")"
      else
        eval "$(dircolors --sh)"
      fi
    fi

    alias ls="${aliases[ls]:-ls} --color=auto"
  else
    alias ls="${aliases[ls]:-ls} -F"
  fi
else
  # BSD Core Utilities
  if zstyle -t ':prezto:module:utility:ls' color; then
    # Define colors for BSD ls if they're not already defined
    if [[ -z "$LSCOLORS" ]]; then
      export LSCOLORS='exfxcxdxbxGxDxabagacad'
    fi

    # Define colors for the completion system if they're not already defined
    if [[ -z "$LS_COLORS" ]]; then
      export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=36;01:cd=33;01:su=31;40;07:sg=36;40;07:tw=32;40;07:ow=33;40;07:'
    fi

    alias ls="${aliases[ls]:-ls} -G"
  else
    alias ls="${aliases[ls]:-ls} -F"
  fi
fi

alias ll='ls -lh'        # Lists human readable sizes.
alias la='ll -A'         # Lists human readable sizes, hidden files.
